/*
 * Copyright 2000-2017 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    ext.kotlinVersion='1.1.3-2'
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'http://repository.jetbrains.com/repo/'}
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.2.14'
}

version = '0.1'
if (System.hasProperty('build.number')) {
    version = "$version.${System.getProperty('build.number')}"
}
repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'http://repository.jetbrains.com/repo/'}
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'kotlin'
apply plugin: 'idea'

intellij {
    version '14.1.7'
    pluginName 'intellij-kapacitor'
    sandboxDirectory="${project.projectDir}/idea-sandbox"
    updateSinceUntilBuild = false
}

// To prevent weird classpath problems in idea
clean {
    delete "${intellij.sandboxDirectory}/plugins/${intellij.pluginName}"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
}

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

sourceSets {
    main {
        java {
            srcDirs 'src/java', 'gen'
        }
        kotlin {
            srcDir 'src/kotlin'
        }
        resources {
            srcDir 'res'
        }
    }
    test {
        java {
            srcDir 'test/java'
        }
        kotlin {
            srcDir 'test/kotlin'
        }
        resources {
            srcDir 'test-data'
        }
    }
}

if (System.getenv('TEAMCITY_VERSION') != null) {
    test.ignoreFailures = true
} else {
//test.testLogging.showStandardStreams = true
}

idea {
    module {
        excludeDirs += file(intellij.sandboxDirectory)
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}
